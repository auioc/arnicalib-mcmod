static def getEnvAsBoolean(String name) {
    return System.getenv(name) && System.getenv(name).toBoolean()
}

def getExecOutput(List<String> commands) {
    def out = new ByteArrayOutputStream()
    exec {
        commandLine commands
        standardOutput out
    }
    return out.toString().trim()
}

def isCi = getEnvAsBoolean('CI')
def isGithubActions = isCi && getEnvAsBoolean('GITHUB_ACTIONS')
def hasGit = project.file('.git').isDirectory()

def revision = (() -> {
    if (isGithubActions) {
        return System.getenv('GITHUB_SHA')
    } else if (hasGit) {
        return getExecOutput(['git', 'rev-parse', '--verify', 'HEAD'])
    }
    return 'unknown'
})()
def revisionShort = revision.length() > 8 ? revision.substring(0, 7) : revision
def buildNumber = isGithubActions ? System.getenv('GITHUB_RUN_NUMBER').toInteger() : 0
def isDirty = hasGit ? !getExecOutput(['git', 'status', '--short']).isEmpty() : false
def isRelease = isGithubActions ? getEnvAsBoolean('IS_RELEASE') : false
def buildInfo = "rev.${revisionShort}-build.${buildNumber}${isRelease ? '' : '-dev'}${isDirty ? '-dirty' : ''}"

project.group = mod_group_id
project.base {
    archivesName = mod_id
}
project.version = getEnvAsBoolean('IS_RELEASE')
    ? "${minecraft_version}-${mod_version}"
    : "${minecraft_version}-${mod_version}-${buildInfo}"

sourceSets.main.java { srcDir "$buildDir/generated/java" }

def package_name = "${mod_group_id}.${mod_id}"
def template = """
package ${package_name};
public final class BuildInfo {
    public static final String MINECRAFT_VERSION = "${minecraft_version}";
    public static final String NEOFORGE_VERSION = "${neo_version}";
    public static final String MOD_VERSION = "${mod_version}";
    public static final boolean IS_CI_BUILD = ${isCi};
    public static final String REVERSION = "${revision}";
    public static final int BUILD_NUMBER = ${buildNumber};
    public static final boolean IS_RELEASE = ${isRelease};
    public static final boolean IS_DIRTY = ${isDirty};
}
"""

compileJava {
    doFirst {
        file("$buildDir/generated/java/").mkdirs()
        file("$buildDir/generated/java/BuildInfo.java").write(template)
        logger.lifecycle("Build info generated")
        logger.info(template)
    }
}



